apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // logger
    implementation "org.slf4j:slf4j-api:${dependency_slf4j_version}"

    implementation "com.android.support:appcompat-v7:${dependency_support_v7_version}"
    implementation "com.android.support:support-v4:${dependency_support_v4_version}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

androidExtensions {
    experimental = true
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        debugAAR(MavenPublication) {
            groupId group
            artifactId 'api'
            version version
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/api-debug.aar")

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')

                final d0 = dependencies.appendNode('dependency')
                d0.appendNode('groupId', 'org.slf4j')
                d0.appendNode('artifactId', 'slf4j-api')
                d0.appendNode('version', "${dependency_slf4j_version}")

                final d1 = dependencies.appendNode('dependency')
                d1.appendNode('groupId', 'com.android.support')
                d1.appendNode('artifactId', 'support-v4')
                d1.appendNode('version', "${dependency_support_v4_version}")

                final d2 = dependencies.appendNode('dependency')
                d2.appendNode('groupId', 'org.jetbrains.kotlin')
                d2.appendNode('artifactId', 'kotlin-stdlib-jdk7')
                d2.appendNode('version', "$kotlin_version")
            }
        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}


